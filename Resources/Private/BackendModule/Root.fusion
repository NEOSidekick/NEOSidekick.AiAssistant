include: resource://Neos.Fusion/Private/Fusion/Root.fusion
include: resource://Neos.Neos.Ui/Private/Fusion/Prototypes/RenderConfiguration.fusion
include: resource://Sitegeist.FusionLinkPrototypes/Private/Fusion/Root.fusion
include: ./BackendModule.fusion

NEOSidekick.AiAssistant.BackendModule.ImageAlternativeTextController.index = NEOSidekick.AiAssistant:BackendModule {
    getAction = 'findAssets'
    updateAction = 'updateAssets'
    moduleConfiguration = Neos.Fusion:Value {
        defaults = Neos.Fusion:DataStructure {
            itemType = 'Asset'
            onlyAssetsInUse = false
            propertyName = 'title'
            itemsPerPage = 10
            language = ${Configuration.setting('NEOSidekick.AiAssistant.defaultLanguage')}
        }
        config = ${Array.filter(Configuration.setting('NEOSidekick.AiAssistant.altTextGeneratorModule'), (value, key) => value != null)}
        enforceConfigs = ${Array.keys(Array.filter(this.config, (value, key) => key != 'itemsPerPage'))}
        editableProperties = ${this.config.propertyName ? [this.config.propertyName] : ['title']}
        value = ${Array.concat(this.defaults, this.config, {enforceConfigs: this.enforceConfigs}, {editableProperties: this.editableProperties})}
    }
}

NEOSidekick.AiAssistant.BackendModule.FocusKeywordController.index = NEOSidekick.AiAssistant:BackendModule {
    getAction = 'findDocumentNodes'
    updateAction = 'updateNodeProperties'
    moduleConfiguration = Neos.Fusion:Value {
        defaults = Neos.Fusion:DataStructure {
            // moduleName is only used for the configuration form text
            moduleName = 'FocusKeyword'
            itemType = 'DocumentNode'
            workspace = 'live'
            propertyFilter = null
            propertyFilterOptions = ${['none', 'only-empty-focus-keywords', 'only-existing-focus-keywords']}
            nodeTypeFilter = null
            baseNodeTypeFilter = 'NEOSidekick.AiAssistant:Mixin.AiPageBriefing'
            itemsPerPage = 10
            editableProperties = ${['focusKeyword']}
            actions = Neos.Fusion:DataStructure
        }
        config = ${Array.filter(Configuration.setting('NEOSidekick.AiAssistant.focusKeywordGeneratorModule'), (value, key) => value != null)}
        enforceConfigs = ${Array.keys(Array.filter(this.config, (value, key) => key != 'itemsPerPage'))}
        value = ${Array.concat(this.defaults, this.config, {enforceConfigs: this.enforceConfigs})}
    }
}

NEOSidekick.AiAssistant.BackendModule.SeoTitleAndMetaDescriptionController.index = NEOSidekick.AiAssistant:BackendModule {
    getAction = 'findDocumentNodes'
    updateAction = 'updateNodeProperties'
    moduleConfiguration = Neos.Fusion:Value {
        defaults = Neos.Fusion:DataStructure {
            // moduleName is only used for the configuration form text
            moduleName = 'SeoTitleAndMetaDescription'
            itemType = 'DocumentNode'
            workspace = 'live'
            propertyFilter = null
            propertyFilterOptions = ${['none', 'only-empty-seo-titles-or-meta-descriptions', 'only-empty-seo-titles', 'only-empty-meta-descriptions', 'only-existing-seo-titles', 'only-existing-meta-descriptions']}
            nodeTypeFilter = null
            baseNodeTypeFilter = 'Neos.Seo:SeoMetaTagsMixin'
            itemsPerPage = 10
            readonlyProperties = ${['focusKeyword']}
            editableProperties = ${['titleOverride', 'metaDescription']}
            showSeoDirectives = true
            actions = Neos.Fusion:DataStructure {
                'autoGenerateEmptySeoTitles' = ${{'active': true, 'propertyName': 'titleOverride', 'clientEval': 'ClientEval: !node.properties.titleOverride'}}
                'autoGenerateEmptyMetaDescriptions' = ${{'active': true, 'propertyName': 'metaDescription', 'clientEval': 'ClientEval: !node.properties.metaDescription'}}
                'autoGenerateExistingSeoTitles' = ${{'active': false, 'propertyName': 'titleOverride', 'clientEval': 'ClientEval: node.properties.titleOverride'}}
                'autoGenerateExistingMetaDescriptions' = ${{'active': false, 'propertyName': 'metaDescription', 'clientEval': 'ClientEval: node.properties.metaDescription'}}
            }
        }
        config = ${Array.filter(Configuration.setting('NEOSidekick.AiAssistant.seoTitleAndMetaDescriptionModule'), (value, key) => value != null)}
        enforceConfigs = ${Array.keys(Array.filter(this.config, (value, key) => key != 'itemsPerPage'))}
        value = ${Array.concat(this.defaults, this.config, {enforceConfigs: this.enforceConfigs})}
    }
}

NEOSidekick.AiAssistant.BackendModule.ConfigurationController.index = Neos.Fusion:Component {
    apiDomain = ${developmentBuild ? 'https://api-staging.neosidekick.com' : 'https://api.neosidekick.com'}
    renderer = afx`
        <div @if={developmentBuild} style="font-size: 1.5em; font-weight: bold; background: orange; padding: 1rem; margin-bottom: 1rem;">You are using the Development Version of the API</div>
        <iframe style="width: 100%; min-height: 1000px; height: 100%; background: #3f3f3f;" src={props.apiDomain + '/chat/account-context/edit?apikey=' + apiKey}></iframe>
    `
}
