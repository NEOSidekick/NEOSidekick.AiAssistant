include: resource://Neos.Fusion/Private/Fusion/Root.fusion
include: resource://Neos.Neos.Ui/Private/Fusion/Prototypes/RenderConfiguration.fusion

NEOSidekick.AiAssistant.AssetModuleController.index = Neos.Fusion:Component {
    endpoints = Neos.Fusion:DataStructure {
        translations = Neos.Fusion:ActionUri {
            request = ${request.mainRequest}
            package = 'Neos.Neos'
            controller = 'Backend\\Backend'
            action = 'xliffAsJson'
            absolute = true
            arguments = Neos.Fusion:DataStructure {
                locale = ${interfaceLanguage}

                # TODO: dirty hack to not have to re-implement neos:backend.configurationCacheVersion VH
                version = Neos.Fusion:Template {
                    templatePath = 'resource://Neos.Neos.Ui/Private/Templates/Backend/ConfigurationVersion.html'
                    @process.trim = ${String.trim(value)}
                }
            }
        }
        getAssets = Neos.Fusion:ActionUri {
            request = ${request.mainRequest}
            package = 'NEOSidekick.AiAssistant'
            controller = 'BackendService'
            action = 'index'
            absolute = true
            format = 'json'
        }
        updateAssets = Neos.Fusion:ActionUri {
            request = ${request.mainRequest}
            package = 'NEOSidekick.AiAssistant'
            controller = 'BackendService'
            action = 'update'
            absolute = true
            format = 'json'
        }
        @process.json = ${Json.stringify(value)}
    }
    frontendConfiguration = Neos.Neos.Ui:RenderConfiguration {
        path = 'frontendConfiguration'
        @process.subset = ${value['NEOSidekick.AiAssistant']}
        @process.json = ${Json.stringify(value)}
    }
    renderer = afx`
        <script>{'_NEOSIDEKICK_AIASSISTANT_endpoints = ' + props.endpoints}</script>
        <script>{'_NEOSIDEKICK_AIASSISTANT_configuration = ' + props.frontendConfiguration}</script>
        <div id="appContainer"></div>
    `
}
