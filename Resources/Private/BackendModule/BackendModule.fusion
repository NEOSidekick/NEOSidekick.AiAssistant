prototype(NEOSidekick.AiAssistant:BackendModule) < prototype(Neos.Fusion:Component) {
    scope = null
    getAction = null
    updateAction = null

    # internal api
    personalWorkspace = ${Neos.Ui.Workspace.getPersonalWorkspace()}
    allowedTargetWorkspaces = ${Neos.Ui.Workspace.getAllowedTargetWorkspaces()}

    renderer = Neos.Fusion:Component {
        scope = ${props.scope}
        workspaces = ${Array.set(props.allowedTargetWorkspaces, props.personalWorkspace.name, props.personalWorkspace)}
        workspaces.@process.json = ${Json.stringify(value)}
        endpoints = Neos.Fusion:DataStructure {
            overview = Sitegeist.FusionLinkPrototypes:ModuleUri {
                action = 'index'
                absolute = true
            }
            nodeTypeSchema = Sitegeist.FusionLinkPrototypes:ActionUri {
                package = 'Neos.Neos'
                controller = 'Backend\\Schema'
                action = 'nodeTypeSchema'
                absolute = true
                arguments = Neos.Fusion:DataStructure {
                    # TODO: dirty hack to not have to re-implement neos:backend.configurationCacheVersion VH
                    version = Neos.Fusion:Template {
                        templatePath = 'resource://Neos.Neos.Ui/Private/Templates/Backend/ConfigurationVersion.html'
                        @process.trim = ${String.trim(value)}
                    }
                }
            }
            translations = Sitegeist.FusionLinkPrototypes:ActionUri {
                package = 'Neos.Neos'
                controller = 'Backend\\Backend'
                action = 'xliffAsJson'
                absolute = true
                arguments = Neos.Fusion:DataStructure {
                    locale = ${interfaceLanguage}

                    # TODO: dirty hack to not have to re-implement neos:backend.configurationCacheVersion VH
                    version = Neos.Fusion:Template {
                        templatePath = 'resource://Neos.Neos.Ui/Private/Templates/Backend/ConfigurationVersion.html'
                        @process.trim = ${String.trim(value)}
                    }
                }
            }
            get = Sitegeist.FusionLinkPrototypes:ActionUri {
                package = 'NEOSidekick.AiAssistant'
                controller = 'BackendService'
                action = ${props.getAction}
                absolute = true
                format = 'json'
            }
            update = Sitegeist.FusionLinkPrototypes:ActionUri {
                package = 'NEOSidekick.AiAssistant'
                controller = 'BackendService'
                action = ${props.updateAction}
                absolute = true
                format = 'json'
            }
            @process.json = ${Json.stringify(value)}
        }
        // TODO: maybe refactor this and not use prototypes
        // from Neos.Neos.Ui to avoid polluting the UI configuration
        // in Neos.Neos.Ui
        frontendConfiguration = Neos.Neos.Ui:RenderConfiguration {
            path = 'frontendConfiguration'
            context.user = ${user}
            @process.subset = ${value['NEOSidekick.AiAssistant']}
            @process.json = ${Json.stringify(value)}
        }

        renderer = afx`
            <script>{'_NEOSIDEKICK_AIASSISTANT_workspaces = ' + props.workspaces}</script>
            <script>{'_NEOSIDEKICK_AIASSISTANT_endpoints = ' + props.endpoints}</script>
            <script>{'_NEOSIDEKICK_AIASSISTANT_configuration = ' + props.frontendConfiguration}</script>
            <div id="appContainer" data-scope={props.scope} data-csrf-token={csrfToken}></div>
        `
    }

    @cache {
        mode = 'embed'
    }
}
