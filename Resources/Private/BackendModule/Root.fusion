include: resource://Neos.Fusion/Private/Fusion/Root.fusion
include: resource://Neos.Neos.Ui/Private/Fusion/Prototypes/RenderConfiguration.fusion
include: resource://Sitegeist.FusionLinkPrototypes/Private/Fusion/Root.fusion

NEOSidekick.AiAssistant.AssetModuleController.index = Neos.Fusion:Component {
    endpoints = Neos.Fusion:DataStructure {
        translations = Sitegeist.FusionLinkPrototypes:ActionUri {
            package = 'Neos.Neos'
            controller = 'Backend\\Backend'
            action = 'xliffAsJson'
            absolute = true
            arguments = Neos.Fusion:DataStructure {
                locale = ${interfaceLanguage}

                # TODO: dirty hack to not have to re-implement neos:backend.configurationCacheVersion VH
                version = Neos.Fusion:Template {
                    templatePath = 'resource://Neos.Neos.Ui/Private/Templates/Backend/ConfigurationVersion.html'
                    @process.trim = ${String.trim(value)}
                }
            }
        }
        getAssets = Sitegeist.FusionLinkPrototypes:ActionUri {
            package = 'NEOSidekick.AiAssistant'
            controller = 'BackendService'
            action = 'index'
            absolute = true
            format = 'json'
        }
        updateAssets = Sitegeist.FusionLinkPrototypes:ActionUri {
            package = 'NEOSidekick.AiAssistant'
            controller = 'BackendService'
            action = 'update'
            absolute = true
            format = 'json'
        }
        @process.json = ${Json.stringify(value)}
    }
    // TODO: maybe refactor this and not use prototypes
    // from Neos.Neos.Ui to avoid polluting the UI configuration
    // in Neos.Neos.Ui
    frontendConfiguration = Neos.Neos.Ui:RenderConfiguration {
        path = 'frontendConfiguration'
        context.user = ${user}
        @process.subset = ${value['NEOSidekick.AiAssistant']}
        @process.json = ${Json.stringify(value)}
    }
    renderer = afx`
        <script>{'_NEOSIDEKICK_AIASSISTANT_endpoints = ' + props.endpoints}</script>
        <script>{'_NEOSIDEKICK_AIASSISTANT_configuration = ' + props.frontendConfiguration}</script>
        <div id="appContainer" data-csrf-token={csrfToken}></div>
    `
}
